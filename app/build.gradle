plugins {
    alias(libs.plugins.android.application)
}
def localProps = new Properties()
def localPropsFile = rootProject.file("local.properties")
if (localPropsFile.exists()) {
    localProps.load(localPropsFile.newDataInputStream())
}
// Prefer local.properties (not committed). Fallback to gradle.properties if provided.
// Use placeholder values if not configured to allow build to succeed
def rk = localProps.getProperty("RAPIDAPI_KEY", project.findProperty("RAPIDAPI_KEY") ?: "YOUR_API_KEY_HERE")
def rh = localProps.getProperty("RAPIDAPI_HOST", project.findProperty("RAPIDAPI_HOST") ?: "flickr-api2.p.rapidapi.com")
def rb = localProps.getProperty("RAPIDAPI_BASE_URL", project.findProperty("RAPIDAPI_BASE_URL") ?: "https://flickr-api2.p.rapidapi.com/")
android {
    namespace 'vn.edu.usth.flickrbrowser'
    compileSdk 36

    defaultConfig {
        applicationId "vn.edu.usth.flickrbrowser"
        minSdk 24
        targetSdk 36
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // Provide BuildConfig fields from properties (avoid hardcoding secrets in VCS)
        buildConfigField "String", "RAPIDAPI_BASE_URL", "\"${rb}\""
        buildConfigField "String", "RAPIDAPI_HOST",     "\"${rh}\""
        buildConfigField "String", "RAPIDAPI_KEY",      "\"${rk}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        buildConfig = true
        viewBinding = true
        dataBinding = false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {

    implementation libs.appcompat
    implementation libs.material
    implementation libs.activity
    implementation libs.constraintlayout
    implementation "com.facebook.shimmer:shimmer:0.5.0"
    implementation libs.swiperefreshlayout

    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'

    implementation 'androidx.recyclerview:recyclerview:1.3.2'    // RecyclerView grid
    implementation 'com.google.android.material:material:1.12.0' // AppBar, TextInput
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4' // ConstraintLayout
    implementation 'com.github.bumptech.glide:glide:4.16.0'      // Load áº£nh
    annotationProcessor 'com.github.bumptech.glide:compiler:4.16.0'

    // Lifecycle (LiveData)
    implementation 'androidx.lifecycle:lifecycle-livedata:2.8.4'
    implementation 'androidx.lifecycle:lifecycle-runtime:2.8.4'
    
    // ViewPager2 for gallery swipe
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    
    // PhotoView for zoom and pan
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
}